#!/bin/csh -f

# User params to change input/output behaviour
# Of particular note is the `use_fittp_fix` variable. The original results do NOT use
# this fix, so to match them you must set this to zero. However, data without the fix
# cannot be matched by edges-cal, which does not have this bug.
set refresh_spectra = 0       # If '1' re-read and average spectra even if they already exist.
set use_fittp_fix = 1         # Whether to use the fix to fittp() to use the lowest frequency.
set results_dir = H2Case      # The directory into which to save results. 

# ======================================================================================
# PARAMETERS (Don't change these or you'll change the actual results.)
# ======================================================================================
# NOTE ON FREQUENCY BOUNDS:
#    There are two lower and two upper bounds on frequency defined. The fstart and fstop
#    parameters define the lower/upper frequencies kept at the spectrum-averaging stage.
#    That is, the output files from acqplot7amoon will *only* have frequencies between
#    (fstart, fstop) in them. In the lab-calibration step, the S11's will be modelled
#    between (wfstart, wfstop), but then *evaluated* at the spectral frequencies between
#    (fstart, fstop). The final calibration coefficients will be *fit* between
#    (wfstart, wfstop), but also evaluated between (fstart, fstop) so the output file
#    will have more frequency channels, but the outer frequencies will have a weight of
#    zero.

# Parameters for reading/averaging spectra
set delaystart = 7200         # Don't use the first $delaystart seconds of spectra from loads
set nrfi = 0                  # Use $nrfi terms to fit models to find RFI in spectra
set tcal = 1000               # The load+noise-source temperature assumed as a starting point for calibration
set fstart = 40               # Lower frequency cut for reading spectra. See above notes on frequency bounds.
set fstop = 110               # Upper frequency cut for reading spectra. See above notes on frequency bounds. 
set tstart = 0                # Lowest integer hour of the day to read spectra.
set tstop = 23                # Highest integer hour of the day to read spectra. 23 includes all data. 
set smooth = 8                # Number of channels over which to smooth and decimate.
set calobs=/data5/edges/data/CalibrationObservations/Receiver01/Receiver01_25C_2015_09_02_040_to_200MHz/Spectra
set s11file = ../data/s11_calibration_low_band_LNA25degC_2015-09-16-12-30-29_simulator2_long.txt

# Parameters for edges2k (S11 smoothing and labcal)
# Note that some parameters that were set in the original script are not present here.
# That is because they only affect output plotting, not the actual output data itself.
set wfstart = 50    # Lower frequency bound for *calibration solutions*
set wfstop = 100    # Upper frequency bound for *calibration solutions*
set cfit = 6        # Number of terms used to smooth C1 and C2
set wfit = 5        # Number of terms used to smooth Tunc, Tcos and Tsin 
set tcold = 296     # Assumed temperature of the ambient, short, open loads (i.e. the target of calibration)
set thot = 399      # Assumed temperature of the hot_load (target of calibration)
set nfit2 = 27      # Number of terms used to fit the load S11's (and semi-rigid S-params)
set nfit3 = 11      # Number of terms used to fit the LNA S11
set Lh = -2         # Set the hot-load loss model to come from the semi-rigid cable calculation.
#set wtmode = 0      
set lna_poly = 0    # Set to 0 to force the LNA S11 model to use Fourier series.
# END OF PARAMETERS ====================================================================

if ( $use_fittp_fix ) then
  set results_dir = "$results_dir-fittpfix"
endif

# Make the directory where we'll output the final results of this script.
mkdir -p $results_dir

set acqopts = "-smooth $smooth  -fstart $fstart -fstop $fstop -nrfi $nrfi -tcal $tcal -tstart $tstart -tstop $tstop -delaystart $delaystart"
# Read the (concatenated) ACQ files and write them to txt
if (($refresh_spectra == 1) || !( -e $results_dir/spe_loadr.txt) ) then
echo "Reading Ambient Files... check log at ambient-read.log"
cat $calobs/Ambient_01_2015_245_02_00_00_lab.acq >! temp
cat $calobs/Ambient_01_2015_246_02_00_00_lab.acq >>! temp
../bin/acqplot7amoon temp $acqopts > ambient-read.log
mv spe.txt  $results_dir/spe_loadr.txt
rm temp
endif


if (($refresh_spectra == 1) || !( -e $results_dir/spe_hotr.txt) ) then
echo "Reading HotLoad Files... chekc log at hotload-read.log"
cat $calobs/HotLoad_01_2015_246_04_00_00_lab.acq >! temp
cat $calobs/HotLoad_01_2015_247_00_00_00_lab.acq >>! temp
../bin/acqplot7amoon temp $acqopts > hotload-read.log
mv spe.txt $results_dir/spe_hotr.txt
rm temp
endif

if (($refresh_spectra == 1) || !( -e $results_dir/spe_openr.txt) ) then
echo "Reading Open Cable Files... check log at opencable-read.log"
cat $calobs/LongCableOpen_01_2015_243_14_00_00_lab.acq >! temp
cat $calobs/LongCableOpen_01_2015_244_00_00_00_lab.acq >>! temp
cat $calobs/LongCableOpen_01_2015_245_00_00_00_lab.acq >>! temp
../bin/acqplot7amoon temp $acqopts > opencable-read.log
mv spe.txt $results_dir/spe_openr.txt
rm temp
endif

if (($refresh_spectra == 1) || !( -e $results_dir/spe_shortr.txt) ) then
echo "Reading  Shorted Cable files... check log at shortcable-read.log"
cat $calobs/LongCableShorted_01_2015_241_04_00_00_lab.acq >! temp
cat $calobs/LongCableShorted_01_2015_242_00_00_00_lab.acq >>! temp
cat $calobs/LongCableShorted_01_2015_243_00_00_00_lab.acq >>! temp
../bin/acqplot7amoon temp $acqopts > shortcable-read.log
mv spe.txt $results_dir/spe_shortr.txt
rm temp
endif

if ( $use_fittp_fix ) then
set edges2kbin = "../bin/edges2k_fittpfix"
else
set edges2kbin = "../bin/edges2k"
endif

# SM: added "-lna_poly 0"  to force the LNA fit to be a Fourier series, as that is what
#     Alan said he did originally.  
$edges2kbin -fstart $fstart \
  -spant $results_dir/spe_hotr.txt -spcold $results_dir/spe_loadr.txt \
  -sphot $results_dir/spe_hotr.txt -spopen $results_dir/spe_openr.txt \
  -spshort $results_dir/spe_shortr.txt \
  -cals11 $s11file -cals11_hot $s11file -wfstart $wfstart -wfstop $wfstop -cfit $cfit \
  -wfit $wfit -tcold $tcold -thot $thot \
  -nfit2 $nfit2 -nfit3 $nfit3 -Lh $Lh -lna_poly $lna_poly

set datestr=`date +"%Y-%m-%d_%H-%M"`
cp specal.txt $results_dir/specal_${datestr}.txt
mv specal.txt $results_dir/specal.txt
mv s11_modelled.txt $results_dir/s11_modelled.txt
mv hot_load_loss.txt $results_dir/hot_load_loss.txt
