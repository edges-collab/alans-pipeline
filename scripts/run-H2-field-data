#!/bin/csh -f
# final case2 lowband1 2016_250 - 2017_095 

# The following option overrides lots of other options below, and acts as a master
# switch such that if it is 1, every other option is set to match B18 exactly.
set MASTER_MATCH_B18 = 0

# =========== OPTIONS THAT CAN BE OFTEN CHANGED WHEN DOING COMPARISON / ANALYSIS
set calfile = H2Case-fittpfix/specal.txt  #../results/specal_final_case2.txt
set first_day_read = 616    # 616 to match B18
set last_day = 830          # 830 to match B18  
set do_beam_correction = 1  # should be 1 to match B18

# =========== OPTIONS THAT SHOULDN'T BE CHANGED VERY MUCH
set bindir = /home/smurray/data4/edges/alans-pipeline/bin
set ants11_file = ../data/S11_blade_low_band_2015_342_03_14.txt.csv
set results = H2CaseFieldData
set field_spectra_dir = /data5/edges/data/2014_February_Boolardy/mro/low
set refresh_spectra = 2  # 2 for definitely don't, 0 for auto, 1 for definitely do.
set refresh_calspec = 0  # 2 for definitely don't, 0 for auto, 1 for definitely do.
set use_fittp_fix = 1   # set to zero to not use the fix (to obtain original)
set first_day_cal = $first_day_read  # make it 616 if you want to include all days in the cal

# ============ DON'T TOUCH THESE OPTIONS -- USE MASTER SWITCH TO MATCH B18
if ( $MASTER_MATCH_B18 == 1 ) then
    set calfile = ../results/specal_final_case2.txt
    set do_beam_correction = 1
    set ants11_file = ../data/S11_blade_low_band_2015_342_03_14.txt.csv
    set results = H2CaseFieldData
    set field_spectra_dir = /data5/edges/data/2014_February_Boolardy/mro/low
    set refresh_spectra = 0  # 2 for definitely don't, 0 for auto, 1 for definitely do.
    set refresh_calspec = 1  # 2 for definitely don't, 0 for auto, 1 for definitely do.
    set use_fittp_fix = 0   # set to zero to not use the fix (to obtain original)
endif

if ( $do_beam_correction == 0 ) set results = "${results}NoBeam"

echo "Working in $results, with options"
echo "  calfile: $calfile"
echo "  first_day_read: $first_day_read"
echo "  last_day: $last_day"
echo "  do_beam_correction: $do_beam_correction"
echo "  refresh_spectra: $refresh_spectra"
echo "  refresh_calspec: $refresh_calspec"
echo "  use_fittp_fix: $use_fittp_fix"


# ======================================================================================
# THE SCRIPT (Don't change unless you know what you're doing)
# ======================================================================================
# First, we read all the field spectra and average them.
set day = $first_day_read

if ($refresh_spectra < 2) then
    while ($day <= $last_day)

        set outdir = $results/$day
        set outfile = spegva.txt
        
        if ( ($refresh_spectra == 1) || !( -e $outdir/$outfile) ) then
            mkdir -p $outdir
            cd $outdir

            if ( -e $outfile ) rm $outfile

            if($day <= 366) set day2 = ` echo $day |awk '{printf("%03d",$1)} '`
            if($day <= 366) set year = 2015
            if($day > 366 && $day <= 732) set day2 = ` echo $day |awk '{printf("%03d",$1-366)} '`
            if($day > 366 && $day <= 732) set year = 2016
            if($day > 732) set day2 = ` echo $day |awk '{printf("%03d",$1-732)} '`
            if($day > 732) set year = 2017

            set acqopts = "-gha 12 -dgha 6 -rfi 2.5 -tcal 1000 -pfit 37 -fstart 40 "
            set acqopts = "$acqopts -fstop 100 -smooth 8 -pkpwrm 40 -peakpwr 3 -minpwr 0.7 "
            set acqopts = "$acqopts -nrfi 4 -dloadmax 1000 -adcov 0.4 -maxrmsf 200 -maxfm 200"

            echo Reading/Averaging $year\_$day2 to $outdir/$outfile
            if($year == 2015) continue
            if($year == 2016 || $year == 2017) cat $field_spectra_dir/$year/$year\_$day2* >>! temp_20
            rm temp
            
            $bindir/acqplot7amoon temp_20 $acqopts  >>! temp

            if ( -e spe.txt ) then
                mv spe.txt $outfile
            else
                echo "No data accepted, creating dummy outfile"
                touch $outfile
            endif

            rm temp_20
            cd ../..
        else
            echo $outdir/$outfile exists already, skipping.
        endif


        @ day = $day + 1
        
    end
else
    echo Skipping reading/averaging of field spectra.
endif

# --------------------- CALIBRATION ----------------------------------------------------
if ( $do_beam_correction == 1 ) then
    set skymode = 384
else
    set skymode = -1
endif

# Now, we calibrate the spectra from each day.
set day = $first_day_cal

set caloptions = "-s11ant ../../$ants11_file -mfit 3 -wfstart 51 -wfstop 99 -lmode 6 -tant 296"
set caloptions = "$caloptions -skymode $skymode -antaz 354 -wtmode 100 -nfit4 10 -smooth -8 -low 1"
set caloptions = "$caloptions -mdd 4 -delayant  0.0e-12 -adb 0.0 -cmb 2 -ldb 0.0 -delaylna 0e-12"
set caloptions = "$caloptions -adb 0.0 -test 0 -eoramp -0.5 -eorwid 0 -eorcen 78 -tau 7"

echo Calibrating Field Data with options:
echo    $caloptions

if ( -e spesum2.txt) rm spesum2.txt

if ( $use_fittp_fix == 1 ) then
    set calexc = edges2k
else
    set calexc = edges2k_fittpfix
endif

while ($day <= $last_day)
    cd $results/$day

    cp ../../$calfile specal.txt
    cp ../../../data/newniv.txt azelq.txt # used for H2
    cp ../../../data/408-all-noh .

    set outfile = specavg_cal.txt

    if ( ($refresh_calspec < 2) & ( ($refresh_calspec == 1) || !( -e $outfile) ) ) then
        echo Calibrating Day $day
        $bindir/$calexc -fstart 50 -fstop 100 -spant spegva.txt $caloptions
        mv spe0.txt $outfile
    endif

    rm specal.txt
    rm azelq.txt
    rm 408-all-noh
    
    cat $outfile >>! ../../spesum2.txt
    @ day = $day + 1
    cd ../..
end


# Perform the average over days
set avgopts = "-lim 0.17 -nfit 5  -dmax 0.5  -fstart 51 -fstop 99 -schk 0 -tchk 200 "
set avgopts = "$avgopts -rfi 1.9 -g10  -date -seor -1 -tau 7 -sig 0 -md 1 -out 3"
$bindir/longav spesum2.txt $avgopts
set datestr=`date +"%Y-%m-%d_%H-%M"`
mv spesum2.txt $results/spesum2_${datestr}.txt

echo "Moving final average to $results/final_average_${datestr}.txt and $results/final_average_latest.txt"

cp spe0.txt $results/final_average_${datestr}.txt
mv spe0.txt $results/final_average_latest.txt
# Don't remove the last line, it's necessary for some reason...